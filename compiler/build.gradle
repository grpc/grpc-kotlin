plugins {
    id 'com.google.protobuf' version '0.8.11'

    // Generate IntelliJ IDEA's .idea & .iml project files
    // Starting with 0.8.4 of protobuf-gradle-plugin, *.proto and the gen output files are added
    // to IntelliJ as sources. It is no longer necessary to add them manually to the idea {} block
    // to jump to definitions from Java and Kotlin files.
    // For best results, install the Protobuf and Kotlin plugins for IntelliJ.
    id 'idea'

    // Provide convenience executables for trying out the examples.
    id 'application'
    id 'maven-publish'
}

description = 'The protoc plugin for gRPC Kotlin'
mainClassName = 'io.grpc.kotlin.generator.GeneratorRunner'

repositories {
    google()
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    // Kotlin and Java
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:${rootProject.ext.coroutinesVersion}"
    compile "javax.annotation:javax.annotation-api:1.2"

    // Grpc and Protobuf
    compile project(':grpc-kotlin-stub')
    compile "com.google.protobuf:protobuf-java:${rootProject.ext.protobufVersion}"
    compile "io.grpc:grpc-protobuf:${rootProject.ext.grpcVersion}"
    compile "io.grpc:grpc-stub:${rootProject.ext.grpcVersion}"
    compile "io.grpc:grpc-netty-shaded:${rootProject.ext.grpcVersion}"

    // Misc
    compile 'com.squareup:kotlinpoet:1.5.0'
    compile "org.jetbrains.kotlin:kotlin-reflect:1.3.61"
    compile "com.google.truth:truth:1.0.1"

    // Testing
    testImplementation "com.google.guava:guava:28.2-jre"
    testImplementation "com.google.jimfs:jimfs:1.1"
    testImplementation 'com.google.protobuf:protobuf-gradle-plugin:0.8.11'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.5.2"
    testImplementation "org.mockito:mockito-core:2.28.2"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${rootProject.ext.protobufVersion}" }
    plugins {
        // Specify protoc to generate using kotlin protobuf plugin
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${rootProject.ext.grpcVersion}"
        }
        // Specify protoc to generate using our grpc kotlin plugin
        grpckt {
            path = "$rootDir/compiler/build/install/protoc-gen-grpc-kotlin/bin/protoc-gen-grpc-kotlin"
        }
    }
    generateProtoTasks {
        all().each { task ->
            if (task.name.startsWith('generateTestProto')) {
                task.dependsOn { installDist }
            }
            task.plugins {
                // Generate Java gRPC classes
                grpc { }
                // Generate Kotlin gRPC using the custom plugin from library
                grpckt { }
            }
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}
